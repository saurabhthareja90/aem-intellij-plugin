buildscript {
  ext.kotlin_version = '1.0.5-3'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'org.jetbrains.intellij' version "0.0.39"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'jacoco'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

sourceSets {
  main {
    java.srcDirs = ['src', 'gen']
    kotlin.srcDirs = ['src']
    resources.srcDirs = ['resources']
  }

  test.java.srcDirs += 'test/src'
  test.resources.srcDirs += 'test/data'
}

intellij {
  version = 'IU-2016.2.5'
  plugins = ['JavaScriptLanguage', 'CSS']

  updateSinceUntilBuild = false

  sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

idea {
  module {
    generatedSourceDirs += file('gen')
  }
}

repositories {
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile 'org.mockito:mockito-core:2.2.21'
  testCompile 'org.assertj:assertj-core:3.6.0'
  testCompile 'com.tngtech.java:junit-dataprovider:1.12.0'
}
